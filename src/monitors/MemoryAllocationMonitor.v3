// Copyright 2024 Wizard Authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def monitor_ = MonitorRegistry.add(
    "malloc", "Tracks memory allocation and deallocation.",
    MemoryAllocationMonitor.new());
def allocfilter_ = monitor_.addOption("alloc|a", "filter for memory allocation functions");
def freefilter_ = monitor_.addOption("free|f", "filter for memory free functions");

/** uuid for the next allocation */
var next_id = 1u64;
var alloc_table = HashMap<u64, AllocCallResult>.new(int.!<u64>, u64.==);
var free_table = HashMap<u64, FreeCallResult>.new(int.!<u64>, u64.==);
var addr_to_id_table = HashMap<u32, u64>.new(int.!<u32>, u32.==);
var last_free_id = 0u64;

class MemoryAllocationMonitor extends Monitor {

    var alloc_filter: DeclFilter;
    var free_filter: DeclFilter;

    // Parse monitor arguments to get malloc and free function filters
    def configure(args: string) -> string {
        def argMap = MonitorUtil.argMapFrom(args);
        var has_alloc = false;
        var has_free = false;
        if ((argMap.has("a") || argMap.has("alloc"))) {
            var alloc_arg: string = if (argMap.has("a"), argMap["a"], argMap["alloc"]);
            alloc_filter = DeclFilters.parseString(alloc_arg);
            has_alloc = true;
        }
        if ((argMap.has("f") || argMap.has("free"))) {
            var free_arg = if (argMap.has("f"), argMap["f"], argMap["free"]);
            free_filter = DeclFilters.parseString(free_arg);
            has_free = true;
        }
        if (has_alloc && has_free) {
            return null;
        } else {
            def buf = StringBuilder.new();
            buf.put1("invalid argument(s) \"%s\"", args);
            return buf.toString();
        }
    }

    def onParse(module: Module) {
        var mm = ModuleInstrumenter.new(module);
        if (alloc_filter != null) {
            mm.forEachFuncFiltered(alloc_filter, instrumentAlloc);
        }
        if (free_filter != null) {
            mm.forEachFuncFiltered(free_filter, instrumentFree);
        }
    }

    def onFinish(i: Instance, r: Result) {
        var alloc_ids = Lists.toArray(Maps.keyList(alloc_table));
        alloc_ids = Arrays.sort(alloc_ids, 0, alloc_ids.length, u64.<);

        for (id in alloc_ids) {
            if (free_table.has(id)) {
                continue;
            }

            var alloc_result = alloc_table[id];
            output_unfreed_alloc(id, alloc_result.addr);
        }
    }

    private def instrumentAlloc(mm: ModuleInstrumenter, func: FuncDecl) {
        Instrumentation.insertLocalProbe(mm.module, func.func_index, 0, AllocEnterProbe.new(mm.module, func));
        for (bi = BytecodeIterator.new().reset(func); bi.more(); bi.next()) {
            match (bi.current()) {
                RETURN, RETURN_CALL, RETURN_CALL_INDIRECT, RETURN_CALL_REF => {
                    Instrumentation.insertLocalProbe(mm.module, func.func_index, bi.pc, AllocExitProbe.new());
                }
                _ => ;
            }
        }
        Instrumentation.insertLocalProbe(mm.module, func.func_index, func.cur_bytecode.length - 1, AllocExitProbe.new());
    }

    private def instrumentFree(mm: ModuleInstrumenter, func: FuncDecl) {
        Instrumentation.insertLocalProbe(mm.module, func.func_index, 0, FreeEnterProbe.new(mm.module, func));
    }
}

private class AllocEnterProbe(module: Module, func: FuncDecl) extends Probe {
    def fire(dynamicLoc: DynamicLoc) -> Resumption {
        // size should be the first local
        var accessor = dynamicLoc.frame.getFrameAccessor();
        var size = Values.v_u(accessor.getLocal(0));
        var id = next_id;

        // get caller pc
        var caller_pc = 0;
        var caller_func: FuncDecl;
        match (accessor.caller()) {
            Wasm(f, pc, _) => {
                caller_func = f.decl;
                caller_pc = pc;
            }
            _ => ;
        }

        var ctx = CallContext(module, caller_func, caller_pc);

        // addr is filled in exit probe
        alloc_table[id] = AllocCallResult(ctx, 0, size);
        return Resumption.Continue;
    }
}

private class AllocExitProbe extends Probe {
    def fire(dynamicLoc: DynamicLoc) -> Resumption {
        // address should be on top of the stack before return
        var addr_op = dynamicLoc.frame.getFrameAccessor().getOperand(0);
        var addr = Values.v_u(addr_op);
        var id = next_id;

        addr_to_id_table[addr] = id;

        var res = alloc_table[id];
        var alloc_result = AllocCallResult(res.ctx, addr, res.size);
        alloc_table[id] = alloc_result;

        output_alloc_record(res.ctx, res.size, addr, id);

        next_id += 1;  // Increment the ID here
        return Resumption.Continue;
    }
}

private class FreeEnterProbe(module: Module, func: FuncDecl) extends Probe {
    // output on free exit
    def fire(dynamicLoc: DynamicLoc) -> Resumption {
        var accessor = dynamicLoc.frame.getFrameAccessor();
        var addr = Values.v_u(accessor.getLocal(0));

        // if not found, id should be zero
        var id = 0u64;
        if (addr_to_id_table.has(addr)) {
            id = addr_to_id_table[addr];
        }

        var caller_pc = 0;
        var caller_func: FuncDecl;
        match (accessor.caller()) {
            Wasm(f, pc, _) => {
                caller_func = f.decl;
                caller_pc = pc;
            }
            _ => ;
        }

        var ctx = CallContext(module, caller_func, caller_pc);
        free_table[id] = FreeCallResult(ctx, addr);

        if (last_free_id == id) {
            // double free
            output_free_record(ctx, addr, 0u64);
        } else {
            output_free_record(ctx, addr, id);
        }

        last_free_id = id;
        return Resumption.Continue;
    }
}

/***** OUTPUT FORMATTING HELPER METHODS (Do not modify) *****/
/*
 * ***Output Format for Grading:***
 *
 * **Allocation Tracking Overview:**
 * Each memory allocation is tracked using a **unique, sequentially incremented id** that starts with 1.
 * This id will be used when invoking the output helper methods as specified below.
 *
 * 1. **Allocation Record Output:**
 *    - Use the helper method `output_alloc_record()` **after** the alloc function completes.
 *    - The argument passed to `output_alloc_record()` **must include the unique id** assigned to the allocation.
 *    - Ensure that the id is **sequentially incremented**, starting from **1**, for each new allocation.
 *
 * 2. **Free Record Output:**
 *    - Use the helper method `output_free_record()` **after** the free function completes.
 *    - The argument passed to `output_free_record()` **must include the id** of the respective allocation call that is being freed.
 *    - If no corresponding allocation call can be mapped, the id should be set to **0**.
 *
 * 3. **Unfreed Allocation Output:**
 *    - After the program execution completes, use the helper method `output_unfreed_alloc_record()` to print all unfreed memory allocations.
 *    - The **unfreed allocations must be printed in ascending order of their allocation id**.
 *
 * ***Important Notes:***
 * - The **correct invocation and order of output** are essential for grading.
 * - The unfreed allocations **must follow** the specified order based on their allocation id.
 * - **Do not invoke any other output functions** besides the methods specified above.
 * - **Double-check your output** to ensure it meets the required format, as incorrect output will result in lost points.
 */

type CallContext(module: Module, func: FuncDecl, pc: int);

/** the result of a memory allocation call */
type AllocCallResult(ctx: CallContext, addr: u32, size: u32);
/** the result of a memory free call */
type FreeCallResult(ctx: CallContext, addr: u32);

def output_location_prefix(ctx: CallContext) {
    Trace.OUT.beginColor(Color.FUNC)
        .mark()
        .put1("<%q", ctx.func.render(ctx.module.names, _))
        .ljustify_mark(22)
        .endColors();
    Trace.OUT.beginColor(Color.FUNC)
        .putc('|');
    Trace.OUT.beginColor(Color.LINE)
        .mark()
        .put1(" +%d", ctx.pc)
        .rjustify_mark(7)
        .endColors();
    Trace.OUT.beginColor(Color.FUNC).puts(">   ").endColors();
}

// Output an allocation event
def output_alloc_record(ctx: CallContext, size: u32, addr: u32, id: u64) {
    output_location_prefix(ctx);
    def success = (addr != 0);
    // Allocation print
    Trace.OUT.beginColor(Color.SOURCE)
        .mark()
        .put1("Alloc(%d)", size)
        .rjustify_mark(14)
        .beginColor(Color.FUNC)
        .puts(" --> ");
    Trace.OUT.beginColor (if (success, Color.TYPE, Color.HIGH))
        .mark()
        .put1("%d", addr)
        .rjustify_mark(10);
    Trace.OUT.beginColor(if (success, Color.SUCCESS, Color.HIGH))
        .mark()
        .put1("[%d]", id)
        .rjustify_mark(10)
        .endColors()
        .ln();
}

// Output a free event
def output_free_record(ctx: CallContext, addr: u32, id: u64) {
    output_location_prefix(ctx);
    def success = (id != 0);
    // Free print
    Trace.OUT.beginColor(Color.COUNT)
        .mark()
        .put1("Free(%d)", addr)
        .rjustify_mark(14)
        .beginColor(Color.FUNC)
        .puts(" --> ");
    if (success) {
        Trace.OUT.beginColor(Color.SUCCESS)
            .mark()
            .put1("[%d]", id)
            .rjustify_mark(10)
            .endColors()
            .ln();
    } else {
        Trace.OUT.beginColor(Color.HIGH)
            .mark()
            .puts("[??]")
            .rjustify_mark(10)
            .endColors()
            .ln();
    }
}

// Output an unfreed allocation event
def output_unfreed_alloc(id: u64, addr: u32) {
    Trace.OUT.beginColor(Color.HIGH)
        .mark()
        .put1("[%d] ", id)
        .rjustify_mark(10);
    Trace.OUT.beginColor(Color.TYPE)
        .mark()
        .put1("%d ", addr)
        .rjustify_mark(10);
    Trace.OUT.beginColor(Color.FUNC)
        .mark()
        .puts("Unfreed Allocation")
        .ln();
}
/******************/

